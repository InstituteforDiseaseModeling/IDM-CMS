// Implementation of Garki model in CMDL
// State variables

#model garki;

X1=1000.0;
X2=0;
Y1=100;
Y2=0;
Y3=0;
X3=0;
X4=0;

// Parameters
// birth and death rate
delta=0.0001;
// human biting rate
a=0.3;
// incubation in human
N=15;
// incubation in mosquito
n=10;
// rate of acquiring high recovery rate
alpha2=.00019;
// susceptiblity to bite success
g=0.097;
// low daily recovery rate
r1=.0023;
// high daily recovery rate
r2=.023;
// rate of losing infectivity
alpha1=.002;
// infection rate
C=[0.2*(1.01+sin(time/365*6.28))];
// Calculation of infection rate
h=[g*(1-exp(-C*Y1/(X1+X2+X3+X4+Y1+Y2+Y3)))];

// Reactions
// Vital Dynamics
deathX1, X1->,[delta*X1];
deathX2, X2->,[delta*X2];
deathY1, Y1->,[delta*Y1];
deathY2, Y2->,[delta*Y2];
deathY3, Y3->,[delta*Y3];
deathX3, X3->,[delta*X3];
deathX4, X4->,[delta*X4];
birth,->X1,[delta*(X1+X2+X3+X4+Y1+Y2+Y3)];
// Infection and Recovery
infectX1,X1->X2,[h*X1];
latencyX2,X2->Y1,100,delay: N;
infectX3,X3->X4,[h*X3];
latencyX4,X4->Y3,100, delay: N;
recoveryY3,Y3->X3,[Y3*h/(exp(h/r2)-1)];
recoveryY2,Y2->X1,[Y2*h/(exp(h/r1)-1)];
lossinfectY1,Y1->Y2,[alpha1*Y1];
acquireimmunityY2,Y2->Y3,[alpha2*Y2];

solitary = 1.0;
loner = 1.0;
loner @ solitary;
hermit,->loner,0.25;